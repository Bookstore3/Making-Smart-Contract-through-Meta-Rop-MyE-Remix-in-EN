# Chapter 1
### Analyzing the source Code

---

Before distributing the smart contract, we are gonna take a look at the code.
In this chapter, we are gonna take a moment to understand the purpose and the flow of the code.
Distributing the smart contract will be dealt in the next chapter.

~~~Solidity
pragma solidity ^0.4.24; //Specifying the compiler version

contract SmartController {

    // Controller structure

    struct Controller {
    address addr;  // Address of the Client
    uint endTime; // End time
    bool status;  // Can use the IoT device only when the status is true.
    }

    address public owner; // Address of the owner (EOA)
    address public iot;   // Address of the IoT device (EOA)

    mapping (uint => Controller) public controllers; // Mapping the Controller structure.
    uint public numPaid; //Counting the paid number of times; Used as index number.

    // Checking the address to see if the caller is the owner.
    modifier onlyOwner() {
      require(msg.sender == owner);
      _;
    }

    // Checking the address to see if the caller is the IoT device.
    modifier onlyIoT() {
      require(msg.sender == iot);
      _;
    }

    //Constructor; Take the address of the IoT device as a parameter.
    constructor (address _iot) public { // Must put access modifier
      owner = msg.sender; // Put the adddress of the constructor of this contract
      iot = _iot;         // into owner variable.
      numPaid = 0;
    }

    //Called when the Ether is paid to the contract.
    function payToController() public payable {
      require(msg.value == 1000000000000000000); //If not 1 ETH, return false.
      //Controller construct.
      Controller storage c = controllers[numPaid++]; //Construct as a storage.
      c.addr = msg.sender;
      c.endTime = now + 60; // 1 minute of usage time
      c.status = true;
    }

    // Called on a end time to change the status of the client.
    // Key value of the controllers come as a parameter.
    function updateStatus(uint _index) public onlyIoT {

      //Check if index corresponding Controller exist.
      require(controllers[_index].addr != 0);

      //Check if the end time has passed.
      require(now > controllers[_index].endTime);

      controllers[_index].status = false; //Changing the status.
    }

    // Called to withdraw the sent Ethers.
    function withdrawFunds() public onlyOwner {
       require (owner.send(address(this).balance));
    }

    // Called to terminate the contract.
    function kill() public onlyOwner {
      selfdestruct(owner);
    }
}
//made by Heuristic Wave
//changed add to addr
~~~

By taking a quick look on the code, you might figured out that the code is a simple renting contract of the IoT device.

As you can see, the code is neither perfect nor written thoroughly since it's written for the apprentice level developer.
We will discuss the details of the code while explaining the flow of the contract.

**Note:** Before we analyze the code, few things should be known.
- modifier
~~~Solidity
// Checking the address to see if the caller is the owner.
modifier onlyOwner() {
  require(msg.sender == owner);
  _;
}

// Checking the address to see if the caller is the IoT device.
modifier onlyIoT() {
  require(msg.sender == iot);
  _;
}
~~~
As you can see above, there's a key word called _modifier_. You can think of it as a powerful if statement that has been modularized.
Inside the modifier statement, you can see a underline character **\_;**. This character can be considered as a statement which has been checked by the modifier's requirement.
For instance,
~~~Solidity
// Called to terminate the contract.
function kill() public onlyOwner {
  selfdestruct(owner);
}
~~~
This function is called to terminate the contract.
The only one who can call this function should the owner of this contract.
To do so, if you look at the back end of 'function kill() public onlyOwner' you can see there is a **onlyOwner** key word attached.
Through this word, the code will form as below.
~~~Solidity
// Called to terminate the contract.
function kill() public {
  require(msg.sender == owner);
  selfdestruct(owner);
}
~~~
By modularizing the if statement, we don't need to use the if statement as frequently as we did.

- Storage & Memory
If you take a look at the source code, you will find a line where is used to pay an Ether to the contract.
~~~Solidity
//Called when the Ether is paid to the contract.
function payToController() public payable {
  require(msg.value == 1000000000000000000); //If not 1 ETH, return false.
  //Controller construct.
  Controller storage c = controllers[numPaid++]; //Construct as a storage.
  c.addr = msg.sender;
  c.endTime = now + 60; // 1 minute of usage time
  c.status = true;
}
~~~
In this statement, we have to take a look at line number 5, 'Controller storage c = controllers[numPaid++];'. Especially the key word, **storage**.

Inside a computer there are two types of storage space. One is a memory, and another is a storage like a hard disk. Memory only remembers the process state when the computer is running. As soon as the power of the computer goes off, memory lost all it's content. On the other hand, storage keeps the content of the process even if the power of the computer goes off.

Like so, inside the EVM (Ethereum Virtual Machine), which runs the contract, also has a memory and a storage. If you want to hold some values while running the contract, you should store it inside the memory of the EVM. If you want some values to be remain until next time through blockchain, you should store the values inside a storage.

The source code 'Controller storage c = controllers[numPaid++];' contains the information of the clients who paid a Ether to use the IoT device. In order to remember and check the information about the client, the storage is used for further process.

<br>

Now, let's analyze the purpose and the flow of the source code.

In this contract, three object interacts with each other.

1. The owner who distributed the contract.
2. Iot device which will be registered inside the contract.
3. Client who wants to rent the IoT device through the contract.

The flow of the interaction goes as below.

<br>

1. First, the owner deploys the contract.

   Deploying the contract means the construct of the contract, which will call the constructor.
   ~~~Solidity
   constructor (address _iot) public {
    owner = msg.sender;
    iot = _iot;
    numPaid = 0;
   }
   ~~~
   Constructor takes the address of the message sender and puts the address as owner.
   The address of the IoT device, which comes as a parameter of the constructor, is put as iot.
   In order to manage and check the clients who have sent the Ether to the contract, numPaid variable is used.
   It is now set to zero since nobody has sent the Ether yet.

   > **Note:** By the version of the compiler goes up, the method of declaring the constructor has changed.
     It used to use 'function' key word to declare a constructor. However, while writing the name of the contract
     while making a constructor statement, a lot of typing error has been occured which made the contract unuseable.
     In order to fix the problem, the solidity now use 'Construct' key word which does not require the name of the contract.

  <br>

2. Now the contract has been deployed, the client who wants to rent the IoT device will pay the Ether to the contract.

   In order to pay the Ether, the client calls the payToController() function.
   ~~~Solidity
   //Called when the Ether is paid to the contract.
   function payToController() public payable {
     require(msg.value == 1000000000000000000); //If not 1 ETH, return false.
     //Controller construct.
     Controller storage c = controllers[numPaid++]; //Construct as a storage.
     c.addr = msg.sender;
     c.endTime = now + 60; // 1 minute of usage time
     c.status = true;
   }
   ~~~
   First, the function checks the message if the value is 1 Ether.
   If the sent Ether is 1 Ether, it maps the Controller structure inside a storage.
   Since this was the first time the Ether was paid, numPaid has the value of 0 which stores the value controllers[0] in the Controller structure.
   Then it will do ++ process and make numPaid value to 1 for the next client.

   Let's take a look at the source code for a moment. In this code, if the same client keep sends 1 Ether to the contract even if the client's end time has not come, controllers will be keep constructed as controllers[0], controllers[1], controllers[2]...

   In other words, the index (numPaid) will be assigned according to the event of the Ether sent not by the client. The client of the controllers[0] and controllers[1] don't need to be a different client.

   Also, in order to make a fully functional rental system, the code need to block any other client's payment while a client is renting the IoT device, but this is a simple example code to deploy our test smart contract.
   To study the solidity language, please consider checking a lot of well written open source code on the browser.

   Now back to payToController(), the client has paid 1 Ether, allocated Controller structure as a controllers[0]. Inside the Controller structure, the address of the client, end time and rent status, which has been set to true, is saved.
   ~~~Solidity
   struct Controller {
   address addr;  // Address of the Client
   uint endTime; // End time
   bool status;  // Can use the IoT device only when the status is true.
   }
   ~~~

   One important thing to know is the key word '**now**' from endTime = now + 60. This now might looks like it indicates the actual current time. However, it indicates the timeStamp of the block which contains the transaction of the client.

   In other words, the timeStamp value of the later block, needs to have more than a 60 value than **now**. Only then, the end Time condition can be fulfilled.

   In the case if the next generated block was generated faster than a 60 second, we need to wait another block to be generated so that the client's end Time value will be less then the generated block's timeStamp.

   If the Ethereum network stops making new block, there won't be a block with a new timeStamp. It can lead to client renting the IoT device forever.

   For instance, if the client pays 1 Ether to our contract and that transaction got contained in block number 1, which has the timeStamp value of 1000. Then, the endTime of the client will be 1060.

   In this case, if the network stop making the block, endTime will never come.

   Even if the block number 2 was generated, if the timeStamp of the block is 1030, we'll have to wait for the block with a timeStamp value of 1060.

   Like wise, the source code has some issues. The important point here is to understand the key word of the **now**.

<br>

3. If the client's end time has reached, the **IoT device** will change the status of the client into false.
~~~Solidity
// Called on a end time to change the status of the client.
// Key value of the controllers come as a parameter.
function updateStatus(uint _index) public onlyIoT {

  //Check if index corresponding Controller exist.
  require(controllers[_index].addr != 0);

  //Check if the end time has passed.
  require(now > controllers[_index].endTime);

  controllers[_index].status = false; //Changing the status.
}
~~~
UpdateStatus() will take the corresponding client's index number as a parameter.
Then it will check if this function is called by IoT through modifier 'onlyIoT'.
After that, it will check if the corresponding index has a client address to check if the client, who has paid one Ether, exist.
If the client exist, it will check the client's end time to see if it is over.
> Like we've seen above, now is not a current time.

If the time is over, it will change the status of the client into false so that client can no longer use the IoT device.

Important thing here is that the subject that calls this function is the IoT device.
It is job for the IoT device to suspend the rent, not the client.
To make this source code more complete, it should have some sort of listener that checks the time every second. However, it is not implemented yet.

<br>

4. Lastly, after all of these interactions between contract and clients, Ether will be saved inside the balance of the contract.
Then, the distributor of the contract and supposely owner of the IoT device will withdraw the Ether from the contract.
This process will be done through withdrawFunds() function.
~~~Solidity
// Called to withdraw the sent Ethers.
function withdrawFunds() public onlyOwner {
   require (owner.send(address(this).balance));
}
~~~
Through withdrawFunds() it will check if the subject calling this function is a owner of this contract through modifier onlyOwner. Then, it will send the stacked balance from this contract to the address of the owner.
>It is recommended to use transfer() function than to use send() function.

<br>

Let's summarize the flow of the contract briefly.

1. **Owner** distributes the contract with an IoT address in it.
2. **Client** sends one Ether to the contract to rent the IoT device.
3. When the client's rent time is over, the **IoT device** makes the client's status to false to stop the rent.
4. The **owner** withdraws the Ether from the contract.

Now, let's distribute the smart contract using this source code.
